; WS2812 protocol receiver for WACCA
; by Akasaka / Genjitsu Labs 2025

; PIN MAPPING
;   0       - data input
; jmp       - SAME PIN AS ABOVE

; Guaranteed High duration
.define public T1 2 
; Value duration
.define public T2 5
; Low duration
.define public T3 3

.program ws2812in
    ; Input cycle start
    mov ISR, NULL   ; clear input shift register
    wait 0 pin 0    ; wait until input pin is low
    ; Now input is low
    ; but what if it's just because we were in the middle of a frame?
    ; Ensure we have a good idle time before proceeding
    pull
ensure_idle:
    mov X, osr
ensure_idle_loop:
    set Y, 31
wait_idle:
    wait 0 pin 0
    jmp pin ensure_idle ; we encountered a high, so restart waiting again
    jmp Y-- wait_idle ; inner loop 32 times
    jmp X-- ensure_idle_loop ; outer loop 32 times

    ; Now we have sure been idling for a while
    ; Ready to receive 24 bits (single pixel color)
wait_bit_start:
    wait 1 pin 0 ; Wait for start of bit
    nop [T1 - 1] ; Delay for duration we know is gonna be high
    jmp pin it_was_high
    ; Jump not taken, so it was low
    ; Shift a 0 into the ISR
    set X, 0
    in X, 1
    jmp wait_bit_end
it_was_high:
    ; We jumped here, so it was high
    set X, 1
    in X, 1
wait_bit_end:
    ; Wait for the low time to begin
    wait 0 pin 0
    ; Again and again
    jmp wait_bit_start